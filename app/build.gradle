plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'androidx.navigation.safeargs.kotlin'
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.2"

    def versionPropsFile = file('version.properties')
    def versionBuild

    defaultConfig {
        applicationId "dev.filipebezerra.apps.horariodonibus"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode versionBuild
        versionName "0.1.0" + versionBuild.toString()
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            buildConfigField "String", "BASE_API_URL", "\"https://simapp.rmtcgoiania.com.br/\""
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField "String", "BASE_API_URL", "\"https://simapp.rmtcgoiania.com.br/\""
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }
    // https://developer.android.com/jetpack/androidx/releases/databinding
    buildFeatures {
        dataBinding true
    }
    packagingOptions {
        // https://github.com/Kotlin/kotlinx.coroutines#avoiding-including-the-debug-infrastructure-in-the-resulting-apk
        exclude 'DebugProbesKt.bin'
        // https://github.com/Kotlin/kotlinx.coroutines/issues/1064#issuecomment-742172419
        // This is a bug in AGP (the Android Gradle Plugin), that is fixed since its version 7.0.0-alpha01
        exclude 'META-INF/atomicfu.kotlin_module'
    }

    if (versionPropsFile.canRead()) {
        def versionProps = new Properties()
        versionProps.load(new FileInputStream(versionPropsFile))
        versionBuild = versionProps['VERSION_BUILD'].toInteger()
    } else {
        throw new FileNotFoundException("Could not read version.properties!")
    }

    ext.autoIncrementBuildNumber = {
        if (versionPropsFile.canRead()) {
            def versionProps = new Properties()
            versionProps.load(new FileInputStream(versionPropsFile))
            versionBuild = versionProps['VERSION_BUILD'].toInteger() + 1
            versionProps['VERSION_BUILD'] = versionBuild.toString()
            versionProps.store(versionPropsFile.newWriter(), null)
        } else {
            throw new FileNotFoundException("Could not read version.properties!")
        }
    }

    gradle.taskGraph.whenReady { taskGraph ->
        if (taskGraph.hasTask(assembleDebug)) {
            autoIncrementBuildNumber()
        } else if (taskGraph.hasTask(assembleRelease)) {
            autoIncrementBuildNumber()
        }
    }
}

dependencies {
    // region Kotlin support
    implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:$rootProject.kotlin_version")
    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-core:$rootProject.kotlin_coroutines_version")
    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-android:$rootProject.kotlin_coroutines_version")
    // endregion

    // region AndroidX dependencies
    // https://developer.android.com/jetpack/androidx/releases/core
    implementation("androidx.core:core-ktx:$rootProject.core_version")
    // https://developer.android.com/jetpack/androidx/releases/appcompat
    implementation("androidx.appcompat:appcompat:$rootProject.appcompat_version")
    // https://developer.android.com/jetpack/androidx/releases/constraintlayout
    implementation("androidx.constraintlayout:constraintlayout:$rootProject.constraintlayout_version")
    // https://developer.android.com/jetpack/androidx/releases/recyclerview
    implementation("androidx.recyclerview:recyclerview:$rootProject.recyclerview_version")
    // https://developer.android.com/jetpack/androidx/releases/viewpager2
    implementation("androidx.viewpager2:viewpager2:$rootProject.viewpager2_version")
    // https://developer.android.com/jetpack/androidx/releases/transition
    implementation("androidx.transition:transition-ktx:$rootProject.transition_version")
    // https://developer.android.com/jetpack/androidx/releases/swiperefreshlayout
    implementation("androidx.swiperefreshlayout:swiperefreshlayout:$rootProject.swiperefreshlayout_version")
    // https://developer.android.com/jetpack/androidx/releases/navigation
    implementation("androidx.navigation:navigation-fragment-ktx:$rootProject.navigation_version")
    implementation("androidx.navigation:navigation-ui-ktx:$rootProject.navigation_version")
    // https://developer.android.com/jetpack/androidx/releases/lifecycle
    implementation("androidx.lifecycle:lifecycle-livedata-ktx:$rootProject.lifecycle_version")
    implementation("androidx.lifecycle:lifecycle-viewmodel-ktx:$rootProject.lifecycle_version")
    implementation("androidx.lifecycle:lifecycle-viewmodel-savedstate:$rootProject.lifecycle_version")
    implementation("androidx.lifecycle:lifecycle-common-java8:$rootProject.lifecycle_version")
    // endregion

    // region Material Design Components
    // https://github.com/material-components/material-components-android/releases
    implementation("com.google.android.material:material:$rootProject.material_version")
    // endregion

    // region Testing dependencies
    testImplementation("junit:junit:$rootProject.junit_version")
    androidTestImplementation("androidx.test.ext:junit:$rootProject.androidx_test_ext_junit_version")
    androidTestImplementation("androidx.test.espresso:espresso-core:$rootProject.espresso_version")
    androidTestImplementation("androidx.navigation:navigation-testing:$rootProject.navigation_version")
    debugImplementation("androidx.fragment:fragment-testing:$rootProject.fragment_version")
    // endregion

    // region Third-party dependencies
    // https://github.com/JakeWharton/timber/blob/master/CHANGELOG.md
    implementation("com.jakewharton.timber:timber:$rootProject.timber_version")

    // https://github.com/square/retrofit/blob/master/CHANGELOG.md
    implementation("com.squareup.retrofit2:retrofit:$rootProject.retrofit_version")
    implementation("com.squareup.retrofit2:converter-moshi:$rootProject.retrofit_version")

    // https://square.github.io/okhttp/changelog/
    implementation(platform("com.squareup.okhttp3:okhttp-bom:$rootProject.okhttp3_version"))
    implementation("com.squareup.okhttp3:okhttp")
    implementation("com.squareup.okhttp3:logging-interceptor")

    // https://github.com/square/moshi/blob/master/CHANGELOG.md
    // https://medium.com/@BladeCoder/advanced-json-parsing-techniques-using-moshi-and-kotlin-daf56a7b963d
    implementation("com.squareup.moshi:moshi:$rootProject.moshi_version")
    kapt("com.squareup.moshi:moshi-kotlin-codegen:$rootProject.moshi_version", {
        exclude group: 'org.jetbrains.kotlin', module: 'kotlin-reflect'
    })

    // TODO: Migrate to API desugaring
    // https://developer.android.com/studio/write/java8-support#library-desugaring
    implementation("net.danlew:android.joda:$rootProject.joda_android_version")
    // TODO: Migrate to MDC ShapeableImageView
    implementation("de.hdodenhof:circleimageview:$rootProject.circleimageview_version")
    // TODO: Try to migrate to MDC ShapeableImageView
    implementation("com.amulyakhare:com.amulyakhare.textdrawable:$rootProject.textdrawable_version")
    // endregion
}